// Attempt at a realtime version with continuous scanning.
// Requires more than one Nav Reciever
// Controls receiver, scanning and storing coordinates.
// Nav receivers should be on `frequency:1` and `listenangle:90`
// A Text Panel with device field `:Location`
// Do not run SkyScan-Display.yolol concurrently, this script will handle its own display.
// Memory chip must be default named (`ChipField1`, `ChipField2`, etc)
// - CF1 is Origin North
// - CF2 is Origin East
// - CF3 is Origin West
// - CF4 is Scan index (for matching coords to a scan run)
// If running locmark.yolol:
// - CF5 is Saved Location 1 Origin North
// - CF6 is Saved Location 1 Origin East
// - CF7 is Saved Location 1 Origin West
// - CF8 is Saved Location 2 Origin North
// - CF9 is Saved Location 2 Origin East
// - CF10 is Saved Location 2 Origin West
// [Optional] A text panel `NavStatus` for additional scan info

ORIGIN_N="origin_north"
ORIGIN_E="origin_east"
ORIGIN_W="origin_west"

// If not active, reset
if :skyscan==0 then
	:navstatus="Inactive"
	:location="Inactive"
	nf=0
	ef=0
	wf=0
	:chipfield1=0
	:chipfield2=0
	:chipfield3=0
end

if :skyscan==1 then 
	:navstatus="Scanning.."
	m=:message
	:targetmessage=0
	// Listen for signals
	if m!=0 then
		// If we get a signal, figure out which
		dist=(1000000 - :signalstrength)/1000
		if m==ORIGIN_N then
			sdata="N:"+dist
			if :chipfield1!=sdata then
				:chipfield1=sdata
				nf++
				:navstatus="Scanning..\n"+m+" detected"
			end
		else if m==ORIGIN_E then
			sdata="E:"+dist
			if :chipfield2!=sdata then
				:chipfield2=sdata
				ef++
				:navstatus="Scanning..\n"+m+" detected"
			end
		else if m==ORIGIN_W then
			sdata="W:"+dist
			if :chipfield3!=sdata then
				:chipfield3=sdata
				wf++
				:navstatus="Scanning..\n"+m+" detected"
			end
		end
	end
	// Attempt to ensure signals are found around same time						
	if nf==ef==wf and nf!=0 then
		:location=:chipfield1+"\n"+:chipfield2+"\n"+:chipfield3
		:navstatus="3 signals acquired"
	else if nf<ef and nf<wf then
		:chipfield1="N:.."
		:navstatus="Acquire "+ORIGIN_N
		:targetmessage=ORIGIN_N
	else if ef<nf and ef<wf then
		:chipfield2="E:.."
		:navstatus="Acquire "+ORIGIN_E
		:targetmessage=ORIGIN_E
	else if wf<nf and wf<ef then
		:chipfield3="W:.."
		:navstatus="Acquire "+ORIGIN_W
		:targetmessage=ORIGIN_W
	end

end