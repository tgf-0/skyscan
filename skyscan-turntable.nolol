// Turntable version with more comprehensive scanning
// Controls receiver, scanning and storing coordinates.
// Also need to run SkyScan-display.yolol for displaying info
// Turntable must have device fields: 
//  - SSTR (Replacing TurretRotation)
//  - SSCTR (Replace TurretCurrentRotation)

// Memory chip must be default named (ChipField1, ChipField2, etc)
// - CF1 = Origin North
// - CF2 = Origin East
// - CF3 = Origin West
// - CF4 = Scan index (for matching coords to a scan run)

// If running locmark.yolol:
// - CF5 = Saved Location 1 Origin North
// - CF6 = Saved Location 1 Origin East
// - CF7 = Saved Location 1 Origin West
// - CF8 = Saved Location 2 Origin North
// - CF9 = Saved Location 2 Origin East
// - CF10 = Saved Location 2 Origin West

// [Optional] A text panel `NavStatus` for additional scan info

ORIGIN_N="origin_north"
ORIGIN_E="origin_east"
ORIGIN_W="origin_west"

macro store(index, data, dist) block
	if data==ORIGIN_N then :chipfield1="N:"+dist+";"+index end

	if data==ORIGIN_E then :chipfield2="E:"+dist+";"+index end
	
	if data==ORIGIN_W then :chipfield3="W:"+dist+";"+index end

end
// If not active, reset everything
if :skyscan==0 then
	:receiverpitch=0
	:sstr=0
	:listenangle=30
	// dmsg=:chipfield1+";"+:chipfield2+";"+:chipfield3+";"+:chipfield4
	// :navinfo=dmsg
	:navstatus="Inactive."
end

// When activated
while :skyscan==1 do
	i=0
	j=-90
	n=0
	// Increment scan index
	:chipfield4++
	scan_index=:chipfield4

	// Begin from Turntable -90째 and Pitch 0째
	while j>=-90 and j<=90 do
		:sstr=j
		// Begin vertical scan
		while i<=90 do
			// Break for reset or skyscan off
			if :skyscan==0 then break end
			
			// Pitch receiver 
			p> :receiverpitch=i
			:navstatus="Scanning:"+:ssctr+"deg,"+:receivercurrentpitch+"deg"
			:navstatus+="\n"+n+" signals found"
			
			// Wait for receiver to actuate
			if :receivercurrentpitch!=i or :ssctr!=j then
				goto p
			end
			
			// Capture messages from origin north, east or west
			if :message==ORIGIN_N or :message==ORIGIN_E or :message==ORIGIN_W then
				n++
				dist=(1000000 - :signalstrength)/1000
				mdata=:message
				store(scan_index,mdata,dist)
			end
			// Pitch in 30째 increments
			i+=30
		end
		// Turn in 30째 increments
		j+=30

		// Sometimes receiverpitch goes past 180 and becomes negative. Why cant they just use 0 to 360 like normal humans?
		if :receivercurrentpitch<0 then
			i=0
			:receiverpitch=0
		end
		if :receivercurrentpitch>=90 then 
			i=0
		end
	end
	// If we past 90 we're probably done with the scan, break out
	if j>90 or j<-90 then 
		:skyscan=0	
	end	
end