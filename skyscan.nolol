// # SKYSCAN: Personal Waypoint Coordinate System

// skycan.yolol: Controls receiver, scanning and storing coordinates.
// ## Requirements: 3 Nav Recievers
// - Nav receivers should be on `frequency:1` and `listenangle:180`
// - Nav receivers should rename message field to nsig, gsig and wsig
// - Nav receivers should rename SignalStrength fields to nss, gss and wss
// - Nav receivers should add values of TargetMessage to "origin_north", "origin_gate", and "origin_west"
// - A Text Panel with device field `:Location`

// - Memory chip must be default named (`ChipField1`, `ChipField2`, etc)
// 	- CF1 is Origin X
// 	- CF2 is Origin Y
// 	- CF3 is Origin Z
// 	- CF4 is no longer used
// If running locmark.yolol:
// 	- CF5 is Saved Location 1 Origin X
// 	- CF6 is Saved Location 1 Origin Y
// 	- CF7 is Saved Location 1 Origin Z
// 	- CF8 is Saved Location 2 Origin X
// 	- CF9 is Saved Location 2 Origin Y
// 	- CF10 is Saved Location 2 Origin Z

// Known distances between transmitters
GW=61195.125
// GE=63232.813
GN=67450.125
// EW=120934

// Transmitter coordinates - origin_gate is the coordinate system (0,0,0)
// Nx=218.365
Nx=0
Ny=GN
// Nz=0
// Ex=-56228.338
// Ey=28926.849
// Ez=0
Wx=GW
// Wy=0
// Wz=0

// If not active, reset
if :skyscan==0 then
	:location="Inactive"
	:chipfield1=0
	:chipfield2=0
	:chipfield3=0
end

while :skyscan==1 do 
	// Get signal strength from each receiver and store it each loop
	nss=:nss;wss=:wss;gss=:gss
	nd=(1000000-nss);wd=(1000000-wss);gd=(1000000-gss)

	// Solve X coord
	x = (gd*gd - wd*wd + GW*GW)/(2*GW)
	// Solve Y coord
	y = (gd*gd - nd*nd + Nx*Nx + Ny*Ny - 2*Nx*x)/(2*Ny)
	// Solve Z coordinate. This is the ONLY line that requires adv chip. 
	// There's no good way around it :[
	zi=(gd*gd) - (x*x) - (y*y) 
	// There are actually two solutions to every calculation. 
	//  Guessing that most players will be Z+
	if zi<0 then zi*=-1 end
	z = sqrt(zi)
	:chipfield3=z;:chipfield2=y;:chipfield1=x
	// Display
	:location="\nX:"+x+"\nY:"+y+"\nZ:"+z
end